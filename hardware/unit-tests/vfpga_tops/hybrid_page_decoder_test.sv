`timescale 1ns / 1ps

`include "lynx_macros.svh"

import parcore::*;
import parcore_test::*;
import libstf::*;

/* -- Tie-off unused interfaces and signals ----------------------------- */
always_comb notify.tie_off_m();
always_comb sq_rd.tie_off_m();
always_comb sq_wr.tie_off_m();
always_comb cq_rd.tie_off_s();
always_comb cq_wr.tie_off_s();

for (genvar I = 1; I < N_STRM_AXI; I++) begin
    always_comb axis_host_recv[I].tie_off_s();
    always_comb axis_host_send[I].tie_off_m();
end

/* -- Fix clock and reset names ----------------------------------------- */
logic clk;
logic rst_n;

assign clk   = aclk;
assign rst_n = aresetn;

/* -- CONFIG ------------------------------------------------------------ */
write_config_i write_configs[1](.*);
read_config_i  read_configs [1](.*);
GlobalConfig #(
    .SYSTEM_ID(PARCORE_SYSTEM_ID),
    .NUM_CONFIGS(1),
    .ADDR_SPACE_SIZES({HYBRID_PAGE_DECODER_CONFIG_REGS})
) inst_config (
    .clk(clk),
    .rst_n(rst_n),

    .axi_ctrl(axi_ctrl),

    .write_configs(write_configs),
    .read_configs(read_configs)
);

ready_valid_i #(data32_t) conf(.*);
HybridPageDecoderConfig inst_hybrid_page_decoder_config (
    .clk(clk),
    .rst_n(rst_n),

    .write_config(write_configs[0]),
    .read_config(read_configs[0]),

    .out(conf)
);

/* -- INPUT ------------------------------------------------------------- */

AXI4S axi_host_recv_0 (.aclk(clk), .aresetn(rst_n));
`AXIS_ASSIGN(axis_host_recv[0], axi_host_recv_0)

ndata_i #(data8_t, 64) in(clk, rst_n);
AXIToNData #(data8_t, 64) inst_axi_to_ndata (
    .clk(clk),
    .rst_n(rst_n),

    .in(axi_host_recv_0),
    .out(in)
);

/* -- OUTPUT ------------------------------------------------------------ */

AXI4S axi_host_send_0 (.aclk(clk), .aresetn(rst_n));
`AXIS_ASSIGN(axi_host_send_0, axis_host_send[0])

ndata_i #(data32_t, 16) out(clk, rst_n);
NDataToAXI #(data32_t, 16) inst_ndata_to_axi (
    .clk(clk),
    .rst_n(rst_n),

    .in(out),
    .out(axi_host_send_0)
);

/* -- DESIGN WIRING ----------------------------------------------------- */

ready_valid_i #(data32_t) confs[1:0](clk, rst_n);
ReadyValidDuplicator #(2) inst_conf_duplicator (
    .clk(clk),
    .rst_n(rst_n),

    .in(conf),
    .out(confs)
);

ndata_i #(data32_t, 16) hybrid_out(clk, rst_n);
HybridPageDecoder #(data32_t, 16) inst_hybrid_page_decoder (
    .clk(clk),
    .rst_n(rst_n),

    .conf(confs[0]),

    .in(in),
    .out(hybrid_out)
);

NormalizeUntil #(data32_t, data32_t, 16) inst_normalize_until (
    .clk(clk),
    .rst_n(rst_n),

    .size(confs[1]),

    .in(hybrid_out),
    .out(out)
);
